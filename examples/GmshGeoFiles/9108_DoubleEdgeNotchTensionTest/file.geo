// -----------------------------------------------------------------------------
//
//  Gmsh GEO
//
// -----------------------------------------------------------------------------

//Use the following line to generate the mesh
//gmsh file.geo  -2 -o mesh.msh

h      = 0.05;  //mesh size
hcrack = 0.002; //mesh size near crack

// ------------------------------------------------------
// ------------------------------------------------------
// A)Geometry Definition: 1)Points 
//                        2)Lines 
//                        3)Curve 
//                        4)Surface 

// ------------------------------------------------------
// A1)Points Definitions: 
//              
//         P7*------------------*P6
//           |                  |
//           |                  |
//           |                  |
//         P8*  \            /  * P5
//                *P9   P4 * 
//        P10*  /            \  * P3
//           |                  |
//           |                  |
//           |                  |
//         P1*------------------*P2
//             
//    |Y
//    |
//    ---X   Dimensions:   2 x 6
//

//           -----Coordinates--
//Points:    ----X,------Y,---Z,
Point(1)   ={ -1.0,   -3.0,   0,  h};
Point(2)   ={  1.0,   -3.0,   0,  h};
Point(3)   ={  1.0, -0.001,   0,  h};
Point(4)   ={  0.5,    0.0,   0,  h};
Point(5)   ={  1.0,  0.001,   0,  h};
Point(6)   ={  1.0,    3.0,   0,  h};
Point(7)   ={ -1.0,    3.0,   0,  h};
Point(8)   ={ -1.0,  0.001,   0,  h};
Point(9)   ={ -0.5,    0.0,   0,  h};
Point(10)  ={ -1.0, -0.001,   0,  h};


// ------------------------------------------------------
// A2)Lines Definition
//
//                    <-L6
//           *------------------*
//           |                  | ^
//        L7 |                  | L5
//           |                  | 
//           *  \L8        L4/  * 
//                *        * 
//           *  /L9        L3\  * 
//           |                  |
//       L10 |                  | ^
//           |                  | L2
//           *------------------*
//                   L1->
//

Line(1) = {1, 2};  //L1:from P1 to P2: P1*--L1-->*P2
Line(2) = {2, 3};
Line(3) = {3, 4};
Line(4) = {4, 5};
Line(5) = {5, 6};
Line(6) = {6, 7};
Line(7) = {7, 8};
Line(8) = {8, 9};
Line(9) = {9, 10};
Line(10) = {10, 1};


// ------------------------------------------------------
// A3)Curve Definition
//
//            Curve 5
//        *----<------*
//        |           |
//        *  \     /  *
//            *   *   
//        *  /     \  * 
//        |           |
//        *----->-----*
//   

Curve Loop(5) = {1,2,3,4,5,6,7,8,9,10};  //C5: through lines L1,L2,...,L7


// ------------------------------------------------------
// A4)Surface Definition
//
//        *----<------*
//        |     S6    |
//        *  \     /  *
//            *   *   
//        *  /     \  * 
//        |           |
//        *----->-----*
//         

Plane Surface(6) = {5};  //  Curve loop 5 C5 --> Surface S6
Recombine Surface {6};



// ------------------------------------------------------
// ------------------------------------------------------
// B)Mesh Generation: 1)Mesh size Box1 
//                    2)Mesh size Box2
//                    3)Mesh min(Box1,Box2)
//                    3)Extrude Mesh 
//                    4)Mesh Algorithm  


// ------------------------------------------------------
// B1) Mesh size Box1
//
//        *-------------------* 
//        |                   | 
//        |                   |
//        |                   |
//        *    -----------    *
//             |hcrack F6|      (Field[6])
//        *    -----------    * 
//        |                   |
//        |                   |
//        |                   |
//        *-------------------*

Field[6]      =    Box;
Field[6].VIn  = hcrack;
Field[6].VOut =      h;

Field[6].XMin =   -0.6;
Field[6].XMax =    0.6;
Field[6].YMin = -0.015;
Field[6].YMax =  0.015;


// ------------------------------------------------------
// B2) Mesh size Box2
//
//        *-------------------* 
//        |                   | 
//        |                   |
//        |                   |
//        |   -------------   |
//        |  |             |  |
//        *  |  hcrack*10  |  *
//        *  |   F7        |  * (Field[7])
//        |  |             |  |
//        |   -------------   |
//        |                   |
//        |                   |
//        |                   |
//        *-------------------*

Field[7]      =       Box;
Field[7].VIn  = hcrack*10;
Field[7].VOut =         h;

Field[7].XMin =      -0.6;
Field[7].XMax =       0.6;
Field[7].YMin =      -0.1;
Field[7].YMax =       0.1;


// ------------------------------------------------------
// B3) Mesh min(Box1,Box2)
//
//        *--------------------* 
//        |                    | 
//        |                    |
//        |                    |
//        |   -------------    |
//        |  |  hcrack*10   |  |
//        *  |   ---------- |  *
//        |  |  | hcrack F8||  | Field[8]
//        *  |   ---------- |  * 
//        |  |              |F8|
//        |   -------------    |
//        |                    |
//        |                    |
//        |                    |
//        *-------------------*

Field[8] = Min;
Field[8].FieldsList = {6,7};
Background Field    = 8;



// ------------------------------------------------------
// B4)Extrude Mesh

//     {X, Y,    Z}    Surface
// Extrude{0, 0,  0.01}{Surface{6}; Layers{{1},{1}}; Recombine;}


// ------------------------------------------------------
// B5)Mesh Algorithm
Geometry.Tolerance = 1e-12;


// ------------------------------------------------------
// Physical groups definition
//
//            "top"
//        *-----------*
//        |           |
//        *  \     /  *
//            *   *   
//        *  /     \  * 
//        |           |
//        *-----------*
//           "bottom"
//
Physical Curve("bottom", 12) = {1};
Physical Curve("top", 13) = {6};
Physical Surface("surface", 14) = {6};
